之前 01 讲中的例子，使用 ChatGPT APT 实现标题翻译、卖点撰写和售价预估的功能。对于运营人员使用的上的，但是让运营人员去学会用 Python 写程序，的确门槛还是太高了。

通过给 ChatGPT prompt，询问如何做一个 Excel 的插件。
![](../images/12-1.png)

```
请你帮我写一个Excel插件，能够将商品标题翻译成英文，寻找商品里面的卖点，以及确定商品的售价
```

可惜，ChatGPT 仅仅告诉可以用翻译的 API。并且编造了一段代码，它实际介绍的 NLTK，spaCy 等等，都是 Python 的开源库，是没有 VBA 代码的。

不过，它提供了一个很有用的信息，Excel 里我们可以通过 VBA 来运行程序。

#### 先来试一试简单的插件任务

```
请你为我写一个Excel的VBA插件，它会将表格里面的第一列的内容，复制到第二列
```

![](../images/12-2.png)

```
Sub CopyFirstColumnToSecondColumn()
    '获取第一列和第二列的范围
    Dim firstColumn As Range
    Dim secondColumn As Range
    Set firstColumn = Range("A1:A" & Cells(Rows.Count, 1).End(xlUp).Row)
    Set secondColumn = Range("B1:B" & Cells(Rows.Count, 1).End(xlUp).Row)

    '复制第一列到第二列
    secondColumn.Value = firstColumn.Value
End Sub
```

根据 ChatGPT 给出的详细操作，这段 VBA 的宏的确能够将我们表格里面的第一列，复制到第二列里面。

我们基本走通了一个基本路径，就怎么在 Excel 里面撰写 VBA 代码并且执行。这个方法也是使用 AI 写代码时一个常用的方式，我们先通过一些简单的任务试一试，看看 AI 是否能把路径走通，还是说在胡扯。

#### 拆解想要完成的代码功能，分步骤让 AI 完成程序

我们在尝试过 VBA 代码可以在 Excel 中正常运行之后，就可以尝试让 ChatGPT 帮我们写正式的代码了。我们不能像一开始随便给它一个模糊的需求，而是应该拆解一下需求。在 prompt 中更准确地描述我们希望 ChatGPT 写出的代码完成什么功能，包括我们希望它使用的具体步骤和方法。

```
请为我写这样一段在Excel里面运行的VBA程序：
1. 读取表格里面的第一列的内容
2. 内容的每一行，都是一个商品标题。对这个商品标题，通过下面的Prompt，调用OpenAI的API拿到返回结果：
"""
Consideration proudct : 商品标题
1. Compose human readable product title used on Amazon in english within 20 words.
2. Write 5 selling points for the products in Amazon.
3. Evaluate a price range for this product in U.S.
Output the result in json format with three properties called title, selling_points and price_range
"""
3. 将对应API的返回结果，插入到表格的第二列里
```

输出结果：

```
Option Explicit
Sub GetOpenAIResults()

    'Declare variables
    Dim wb As Workbook
    Dim ws As Worksheet
    Dim lastRow As Long
    Dim i As Long
    Dim productTitle As String
    Dim sellingPoints As String
    Dim priceRange As String
    Dim outputJSON As String
    Dim response As String
    Dim request As Object
    Dim url As String
    Dim apiKey As String

    'Set variables
    Set wb = ThisWorkbook
    Set ws = wb.ActiveSheet
    lastRow = ws.Cells(ws.Rows.Count, "A").End(xlUp).Row
    apiKey = "YOUR_OPENAI_API_KEY"
    url = "https://api.openai.com/v1/engines/davinci-codex/completions"

    'Loop through each row in column A
    For i = 1 To lastRow

        'Get the product title from column A
        productTitle = ws.Cells(i, 1).Value

        'Set up the OpenAI API request
        Set request = CreateObject("MSXML2.XMLHTTP")
        request.Open "POST", url, False
        request.setRequestHeader "Content-Type", "application/json"
        request.setRequestHeader "Authorization", "Bearer " & apiKey

        'Send the request to OpenAI API
        request.send "{""prompt"": ""Consideration proudct : " & productTitle & vbNewLine & "1. Compose human readable product title used on Amazon in english within 20 words." & vbNewLine & "2. Write 5 selling points for the products in Amazon." & vbNewLine & "3. Evaluate a price range for this product in U.S." & vbNewLine & vbNewLine & "Output the result in json format with three properties called title, selling_points and price_range"",""temperature"": 0.7,""max_tokens"": 1024}"

        'Get the response from OpenAI API
        response = request.responseText

        'Extract the title, selling points, and price range from the response
        productTitle = ExtractJSONValue(response, "title")
        sellingPoints = ExtractJSONValue(response, "selling_points")
        priceRange = ExtractJSONValue(response, "price_range")

        'Create the JSON output string
        outputJSON = "{""title"": """ & productTitle & """,""selling_points"": """ & sellingPoints & """,""price_range"": """ & priceRange & """}"

        'Insert the JSON output into column B
        ws.Cells(i, 2).Value = outputJSON

    Next i

End Sub
Function ExtractJSONValue(jsonString As String, key As String) As String

    'Declare variables
    Dim jsonRegex As Object
    Dim matchCollection As Object
    Dim match As Object

    'Set variables
    Set jsonRegex = CreateObject("VBScript.RegExp")
    jsonRegex.Global = True
    jsonRegex.Pattern = """" & key & """\s*:\s*""([^""]+)"""
    Set matchCollection = jsonRegex.Execute(jsonString)

    'Check if the key was found in the JSON string
    If matchCollection.Count > 0 Then
        Set match = matchCollection(0)
        ExtractJSONValue = match.SubMatches(0)
    Else
        ExtractJSONValue = ""
    End
```

![](../images/12-3.png)
结果还是报错了。。我们的 Excel 里面出现了一个 VBA 的弹窗，有一段错误消息：“缺少 End Function”。

我们直接把错误信息，填到 ChatGPT 的对话窗口里，让它看看怎么解决。

```
……
    Else
        ExtractJSONValue = ""
    End If

End Function
```

![](../images/12-4.png)
输出结果：

```
{"title": "","selling_points": "","price_range": ""}
```

我们再把新的代码贴到 VBA 编辑器里面，再重新运行一次。生成的结果在第二列，只有 JSON 的格式，没有实际的数据。

这个时候我们停下来看一下 ChatGPT 生成的代码到底干了些什么。虽然我们不懂 VBA 的语法，但是根据编程的知识和经验，大概就能够知道 ChatGPT 的这个程序是怎么回事儿了：

1. 整个程序由两个函数组成，GetOpenAIResults 是主函数，是程序的主体执行结构。
   ExtractJSONValue 是一个功能函数，从 API 调用的返回结果里面，通过正则表达式去提取返回结果里面的内容。
2. GetOpenAIResults 这个主函数的结构也非常简单，除了一开始的一系列变量定义，其实就是做了这样几件事情。
   - 通过一个 For 循环，遍历第一列单元格里面的值。
   - 对每一个值，都构造了一个 HTTP 的请求，调用 OpenAI 的 API。
   - 对于拿到的返回结果，通过 ExtractJSONValue 函数提取里面的内容，然后再重新拼装成一个 JSON。
   - 最终将这个 outputJSON 的输出结果，填到第二列的单元格里面。

我们先把调用完 OpenAI 的 API 之后的结果填在第三列。

我们把 GetOpenAIResults 函数最后填入第二列单元格的代码修改一下，把调用 OpenAI 的 API 拿到的返回结果，填到第三列里。

```
……
        ws.Cells(i, 2).Value = outputJSON
        ws.Cells(i, 3).Value = response
……
```

输出结果：

```
{
    "error": {
        "message": "We could not parse the JSON body of your request. (HINT: This likely means you aren't using your HTTP library correctly. The OpenAI API expects a JSON payload, but what was sent was not valid JSON. If you have trouble figuring out how to fix this, please send an email to support@openai.com and include any relevant code you'd like help with.)",
        "type": "invalid_request_error",
        "param": null,
        "code": null
    }
}

```

从这个输出结果里面看到，错误信息是说，OpenAI 不能够解析我们输入的 JSON Body。那么，我们就再修改一下代码，把我们输入的 JSON 放到第四列里，看看是不是这部分代码 ChatGPT 写错了。

```
……
        'Send the request to OpenAI API
        requestJSON = "{""prompt"": ""Consideration proudct : " & productTitle & vbNewLine & "1. Compose human readable product title used on Amazon in english within 20 words." & vbNewLine & "2. Write 5 selling points for the products in Amazon." & vbNewLine & "3. Evaluate a price range for this product in U.S." & vbNewLine & vbNewLine & "Output the result in json format with three properties called title, selling_points and price_range"",""temperature"": 0.7,""max_tokens"": 1024}"
        request.send requestJSON
        'Get the response from OpenAI API
        response = request.responseText
……
        'Insert the JSON output into column B
        ws.Cells(i, 2).Value = outputJSON
        ws.Cells(i, 3).Value = response
        ws.Cells(i, 4).Value = requestJSON
……
```

这个时候我们尝试运行代码的话，VBA 会提示我们“变量未定义”的报错。
![](../images/12-5.png)

```

……
    Dim apiKey As String
    Dim requestJSON As String

    'Set variables
……
```

再次修复问题后，重新运行代码，就会在第四列里，得到我们输入的 JSON 数据。

我们肉眼不容易看出 JSON 是不是合法，只能尝试让 ChatGPT 帮我们看一下这个 JSON 是否合法了。不过 ChatGPT 告诉我们这个 JSON 是合法的，这个时候，我们又一次陷入了僵局。

#### 简化问题寻找思路

我们拿一个最简单的 JSON 来向 OpenAI 发起请求，看看结果是不是还会报出相同的错误。

修改之后仍然报错了，API 调用的返回结果虽然还是个报错。

```
{
    "error": {
        "message": "The model: `davinci-codex` does not exist",
        "type": "invalid_request_error",
        "param": null,
        "code": "model_not_found"
    }
}

```

这个报错告诉我们，对应的 davinci-codex 模型不存在。的确，最近 OpenAI 把 CodeX 的模型下线了，那我们就把模型修改成 text-davinci-003 这个我们之前常用的模型。

```
    url = "https://api.openai.com/v1/engines/text-davinci-003/completions"
```

```
{"id":"cmpl-70ZEVctFduMnv6D1WIz4iffTZdaGl","object":"text_completion","created":1680369791,"model":"text-davinci-003","choices":[{"text":"\n\nI'm doing well, thanks for asking!","index":0,"logprobs":null,"finish_reason":"stop"}],"usage":{"prompt_tokens":4,"completion_tokens":11,"total_tokens":15}}

```

不过这个只是一个调试的回答，我们把使用的 JSON 切换回去

```
……
        'Send the request to OpenAI API
        requestJSON = "{""prompt"": ""Consideration proudct : " & productTitle & vbNewLine & "1. Compose human readable product title used on Amazon in english within 20 words." & vbNewLine & "2. Write 5 selling points for the products in Amazon." & vbNewLine & "3. Evaluate a price range for this product in U.S." & vbNewLine & vbNewLine & "Output the result in json format with three properties called title, selling_points and price_range"",""temperature"": 0.7,""max_tokens"": 1024}"
        request.send requestJSON
        'Get the response from OpenAI API
        response = request.responseText
……
```

很不幸，这一次我们还是拿到了一段和之前相同的报错，告诉我们 JSON 的格式解析不了。

这个时候我们基本确定是我们的 JSON 格式有问题，我们只要能够找到这个 JSON 的格式问题，相信我们离正确答案就不远了。

既然 AI 前面说这个 JSON 格式是合法的，那我们就不妨让它来给我们生成一个同样内容的 VBA 的 JSON 字符串好了，我们也把问题简化，只关心我们的 Prompt 部分。

```
……
        'Send the request to OpenAI API
        requestJSON = "{""prompt"": ""Consideration proudct : " & productTitle & vbNewLine & "1. Compose human readable product title used on Amazon in english within 20 words." & vbNewLine & "2. Write 5 selling points for the products in Amazon." & vbNewLine & "3. Evaluate a price range for this product in U.S." & vbNewLine & vbNewLine & "Output the result in json format with three properties called title, selling_points and price_range"",""temperature"": 0.7,""max_tokens"": 1024}"
        requestJSON = "{""prompt"": ""Consideration proudct : 工厂现货PVC充气青蛙夜市地摊热卖充气玩具发光蛙儿童水上玩具\r\n1. Compose human readable product title used on Amazon in english within 20 words.\r\n2. Write 5 selling points for the products in Amazon.\r\n3. Evaluate a price range for this product in U.S.\r\n\r\nOutput the result in json format with three properties called title, selling_points and price_range""}"
        request.send requestJSON
        'Get the response from OpenAI API
        response = request.responseText
……
```

而这么一运行，我们就得到了一个正常的、有意义的 JSON 返回值。那这个时候，对于问题的定位就进一步缩小了。我们只要看看上下两个字符串有什么不一样就好了。
仔细对比，上下两边用的换行符不一样。上面使用的是 vbnewline，而下面使用的是\r\n。那我们就把上面的换行符设成和下面完全一致的，再来试试看。

```
……
        requestJSON = "{""prompt"": ""Consideration proudct : " & productTitle & "\r\n1. Compose human readable product title used on Amazon in english within 20 words.\r\n2. Write 5 selling points for the products in Amazon.\r\n3. Evaluate a price range for this product in U.S.\r\n\r\nOutput the result in json format with three properties called title, selling_points and price_range.\r\n"",""temperature"": 0.7,""max_tokens"": 1024}"
        request.send requestJSON
……
```

输出结果：

```
{"id":"cmpl-70ZaUPXSb8nU39jmpNkhl59bMsG4A","object":"text_completion","created":1680371154,"model":"text-davinci-003","choices":[{"text":"\r\n\r\n{\r\n    \"title\": \"Premium Handcrafted Aromatherapy Essential Oil Diffuser\", \r\n    \"selling_points\": [\"Natural Ultrasonic Operation\", \"7 Colorful LED Lights\", \"Auto Shutoff Timer\", \"Whisper Quiet Operation\", \"Easy to Clean\"], \r\n    \"price_range\": \"$25 - $50\"\r\n}","index":0,"logprobs":null,"finish_reason":"stop"}],"usage":{"prompt_tokens":89,"completion_tokens":84,"total_tokens":173}}

```

不过，我们的第二列数据的 JSON 还是没有具体内容，只有结构。

#### 让 AI 单独写出提取函数，完成最后的程序

原先 AI 生成的解析 JSON 的代码，使用的是正则表达式，而不是对 JSON 进行反序列化。我们可以直接问 ChatGPT 如何把 JSON 反序列化。

ChatGPT 给了我们示例，告诉我们可以在 GitHub 上找到对应的函数库。如果你按图索骥，就能在 VBA-JSON 和 VBA-Dictionary 找到我们需要的库。并且按照文档的要求，在 VBA 编辑器里通过 “文件”=>“导入文件” 来导入函数库。然后，我们只要按照文档的格式小小修改一下现在的代码，就能提取到我们希望得到的 JSON 格式了。

那接下来，问题就很简单了。我们只需要进一步把这个 JSON 字符串也解析一下，然后把 title、selling_points 以及 price_range 解析出来，分别放到不同的列里面就可以了。如果有想不明白怎么写的代码你还是可以继续问 ChatGPT。

要修改的代码：

```
Option Explicit

Sub GetOpenAIResults()

    'Declare variables
    Dim wb As Workbook
    Dim ws As Worksheet
    Dim lastRow As Long
    Dim i As Long
    Dim productTitle As String
    Dim response As String
    Dim request As Object
    Dim url As String
    Dim apiKey As String
    Dim requestJSON As String
    Dim Json As Object

    'Set variables
    Set wb = ThisWorkbook
    Set ws = wb.ActiveSheet
    lastRow = ws.Cells(ws.Rows.Count, "A").End(xlUp).Row
    apiKey = "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"
    url = "https://api.openai.com/v1/engines/text-davinci-003/completions"


    'Loop through each row in column A
    For i = 2 To lastRow

        'Get the product title from column A
        productTitle = ws.Cells(i, 1).Value

        'Set up the OpenAI API request
        Set request = CreateObject("MSXML2.XMLHTTP")
        request.Open "POST", url, False
        request.setRequestHeader "Content-Type", "application/json"
        request.setRequestHeader "Authorization", "Bearer " & apiKey

        'Send the request to OpenAI API
        requestJSON = "{""prompt"": ""Consideration proudct : " & productTitle & "\r\n1. Compose human readable product title used on Amazon in english within 20 words.\r\n2. Write 5 selling points for the products in Amazon.\r\n3. Evaluate a price range for this product in U.S.\r\n\r\nOutput the result in json format with three properties called title, selling_points and price_range.\r\n"",""temperature"": 0.7,""max_tokens"": 1024}"
        request.send requestJSON
        'Get the response from OpenAI API
        response = request.responseText

        Set Json = JsonConverter.ParseJson(response)
        Set Json = JsonConverter.ParseJson(Json("choices")(1)("text"))

        'Insert the JSON output into column B
        ws.Cells(i, 2).Value = Json("title")
        ws.Cells(i, 3).Value = ConcatenateArrayToString(Json("selling_points"))
        ws.Cells(i, 4).Value = Json("price_range")

    Next i

End Sub



Function ConcatenateArrayToString(arr) As String
    Dim result As String
    Dim i As Long

    'Assuming the array is stored in a variable named "arr"

    For i = 1 To 5
        result = result & arr(i) & vbCrLf  'Use vbCrLf to add a line break after each element
    Next i

    'The "result" variable now contains the concatenated string
    ConcatenateArrayToString = result
End Function

```

最后的代码我们做了几处小的更改。

1. 遍历列表的第一列的时候，我们跳过了第一行的标题行。
2. 去除了不再需要使用的临时变量，以及不再需要的之前 AI 自动生成的函数。
3. selling_points 是一个 5 个元素的数组，通过一个单独的函数封装了从数组到字符串的拼装。

![](../images/12-6.png)
VBA 数组拼接字符串的工作，我们也是让 ChatGPT 指导我们做的

修改之后再执行一下，我们终于得到了理想的结果。

![](../images/12-7.png)
